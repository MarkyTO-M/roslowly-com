//===== Hercules Script ======================================
//= Guild Battle NPC
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= [Official Conversion]
//= Guild Battle with PVP mechanics.
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Optimized. [Euphy]
//============================================================

pvp_y_room,77,85,4	script	Guild Battle Guide	8W_SOLDIER,{

	setarray .@map1x[1],7,9,16,8,20;
	setarray .@map2x[1],91,90,83,91,79;
	setarray .@mapy[1],49,49,50,49,50;

	mes "[Guild Battle Guide]";
	mes " ท่านต้องการจะทำสิ่งใด?";
	mes " ข้าคือผู้แนะนำ Guild Battle";
	mes " สำหรับการ PvP ในแผนที่ใหม่";
	mes " โปรดแจ้งให้ทราบถึงแผนที่";
	mes " ที่คุณต้องการจะไป";
	next;
	set .@i, select("Map 1:Map 2:Map 3:Map 4:Map 5");
	mes "[Guild Battle Guide]";
	mes " คุณได้เลือกแผนที่ "+.@i+".";
	mes " เอาล่ะ, ตอนนี้เลือกทีมที่คุณต้องการ";
	mes " Team 1 หรือ Team 2 ?";
	next;
	set .@j, select("Team 1:Team 2");
	mes "[Guild Battle Guide]";
	mes " เอาล่ะ, ข้าจะย้ายคุณ";
	mes " ไปยังแผนที่ "+.@i+" และเป็นสมาชิกของทีมที่ "+.@j+"";
	mes " คุณพร้อมที่จะเดินทางเลยไหม?";
	next;
	if(select(" แน่นอน: ยกเลิก") == 2) {
		mes "[Guild Battle Guide]";
		mes " โอเค, ข้าหวังว่าจะพบท่านใหม่อีกครั้งในสนามสู้รบ";
		close;
	}
	mes "[Guild Battle Guide]";
	mes " ดีมากๆ, หวังว่านี่จะเป็นช่วงเวลาที่ดี~";
	close2;
	warp "guild_vs"+.@i,getd(".@map"+.@j+"x["+.@i+"]"),.@mapy[.@i];
	end;
}
