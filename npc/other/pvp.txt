//===== Hercules Script ======================================
//= PvP NPCs
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf, Elias (og2)
//===== Current Version: =====================================
//= 2.5
//===== Description: =========================================
//= PvP NPCs that are found in the Inns in major cities.
//= Includes Yoyo Mode, Nightmare Mode, and Event Mode.
//===== Additional Comments: =================================
//= 2.0 Narrators/GateKeepers,etc. Translated from the Official 2011 [Elias]
//= 2.1 Changed ผู้บรรยาย PVP and GateKeeper to Floating NPC [Elias]
//= 2.2 Some cleaning. [Euphy]
//= 2.3 Fixed grammatical errors. [Joseph]
//= 2.4 Fixed incorrect use of 'close' and some cleaning. [Joseph]
//= 2.5 Renewal/Pre-Renewal split. [Euphy]
//============================================================

// Information
//============================================================
-	script	ผู้บรรยาย PVP::pvpe	-1,{
	mes "[ผู้บรรยาย PVP]";
	mes " สวัสดี!";
	mes " ข้ามีหน้าที่ให้คำอธิบาย..";
	mes " โหมด PVP....";
	mes " ผู้บรรยาย PVP!";
	next;
	switch(select(" โหมด PVP เหรอ?"," อะไรคือโหมด PVP?"," กติกาของโหมด PVP เป็นอย่างไร?"," เซฟตำแหน่ง"," สิ้นสุดการสนทนา"))	{
		case 1:
			mes "[ผู้บรรยาย PVP]";
			mes " โหมด PVP ก็คือ";
			mes " 'โหมด Player VS Player '...";
			mes " พูดง่ายๆ ก็คือ";
			mes " ในโลกแห่งจินตนาการ";
			mes " เกมส์ที่ไม่เหมือนใครสำหรับบรรดาผู้เล่นทั้งหลาย...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ได้ต่อสู่กับผู้เล่นคนอื่นแบบออนไลน์";
			mes " เพียงแค่บอก...";
			mes " ^3355FFGate Keeper^000000...";
			mes " ว่าเจ้าอยากจะลอง...";
			mes " เขาก็จะให้เจ้าไปยังจตุรัสการต่อสู้ของโหมด PVP";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ขอให้ระวังตัวให้ดี";
			mes " และต่อสู้ให้สนุกนะ!";
			mes " อีกอย่างหนึ่ง...";
			mes " การเข้าสู่โหมด PVP...";
			mes " ผู้เล่นตะต้องมีเลเวลอย่างน้อย 3";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " เมื่อเจ้ามีคุณสมบัติครบ..";
			mes " ก็ให้เลือกโหมดต่อไปนี้...!";
			mes " โหมด Nightmare";
			mes " และโหมด Monkey";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " เกมในโหมด Nightmare..";
			mes " จะมีกติการเหมือน...!";
			mes " ในโลกจริงๆ";
			mes " เจ้าอาจจะเสียไอเท็ม...";
			mes " และเจ้าอาจจะเสียค่า EXP ด้วย...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " โปรดจำไว้ด้วยว่า..";
			mes " เพื่อหลีกเลี่ยงการเข้าใจผิด!";
			mes " กรุณาคิดให้รอบคอบก่อนที่เจ้าจะเข้าสู่การต่อสู้";
			break;
		case 2:
			mes "[ผู้บรรยาย PVP]";
			mes " การผ่าน 'PVP Gate Keeper'...";
			mes " จำไว้ว่า...";
			mes " ต้องมีเงิน 500 Zeny...";
			mes " เพื่อที่เจ้าจะสามารถผ่านเข้าไปสู่จตุรัสการต่อสู้ในโหมด PVP ได้";
			mes " การก้าวสู่...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " จุตรัสการต่อสู้...";
			mes " ในโหมด Monkey...";
			mes " หรือโหมด Nightmare";
			mes " จุตรัสการต่อสู่...";
			mes " ทั้ง 2 นั้น...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " จะมีผู้บรรยายอยู่...";
			mes " ในโหมด PVP ให้เจ้าด้วย";
			mes " ผู้บรรยายแต่ละคนจะแบ่ง...";
			mes " ผู้เข้าร่วมที่มีเลเวลสูงกวา 31...";
			mes " เป็น 3 กลุ่ม...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ย่อยๆ";
			mes " เจ้าจะมีห้อง PVP 5 ห้อง";
			mes " เจ้าสามารถเลือกได้ว่าจะเข้าห้องไหน...";
			mes " และเข้าไปได้!";
			mes " จากหน้าต่างผู้บรรยาย";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " เจ้าจะพบข้อจำกัดของระดับต่างๆ...";
			mes " และรายละเอียกของกติกาและจำนวนผู้เล่นที่จะต่อสู้กันใน map";
			mes " ทำความเข้าใจกติกานั้นให้ดี";
			mes " ชัยชนะของเจ้าขึ้นอยู่กับมัน";
			mes " จำไว้ว่า...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ทุกๆ map จะมีขีดจำกัด...";
			mes " ของจำนวนผู้เล่น";
			mes " ดังนั้นเจ้าจะเห็น...";
			mes " 'ผู้เข้าร่วม/รวม'";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " และอีกอย่าง...";
			mes " จำไว้ว่า...";
			mes " ในจตุรัสการต่อสู้...";
			mes " เจ้าไม่สามารถ...";
			mes " เซฟตำแหน่งได้";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " จำไว้ให้ดี";
			break;
		case 3:
			mes "[ผู้บรรยาย PVP]";
			mes " เมื่อเจ้าเข้าสู่โหมด PVP....";
			mes " เจ้าจะเห็นตัวเลขอันดับ...";
			mes " อยู่ตรงกลางของหน้าจอ...";
			mes " ของตัวเจ้าเอง/ผู้เล่นคนอื่น...";
			mes " ใน map นั้น";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ซึ่งนั่นน่าจะมีประโยชน์่ต่อเจ้า";
			mes " ค่า EXP ในโหมด PVP เหรอ?";
			mes " จะถูกใช้ในโหมด PVP นี้เท่านั้น";
			mes " จำไว้ให้ดี";
			mes " ค่า EXP";
			mes "ของผู้เล่นทุกคนตอนเริ่มต้นน่ะเหรอ?";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ปรกติจะเป็น 5";
			mes " ถ้าเจ้าชนะ...";
			mes " ซึ่งจะช่วยให้...";
			mes " เจ้าเพิ่มค่า EXP ได้ดีทีเดียว?";
			mes " หากเจ้าแพ้";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " ค่า EXP ของเจ้าจะลงลง 5";
			mes " ระวังให้ดีล่ะ";
			mes " ถ้าเจ้าถูกลงโทษ...";
			mes " และค่า EXP ของเจ้าลดลง...";
			mes " เหลือน้อยกว่า 0...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " เจ้าจะหลุดออกจากโหมด PVP ทันที...";
			mes " และเกมส์ก็จะสิ้นสุดลง!";
			mes " หากค่า EXP ของเจ้ามากกว่า 0...";
			mes " แต่เจ้าดันตาย...";
			mes " เจ้าสามารถของความช่วยเหลือ...";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " จากผู้เล่นคนอื่น...";
			mes " เพื่อกลับ...";
			mes " สู่เกมสได้";
			mes " เข้าใจไหม?";
			next;
			mes "[ผู้บรรยาย PVP]";
			mes " กติกาการต่อสู้...";
			mes " เหมือนกับการต่อสู้ข้างนอก...";
			mes " กติกาเบื้องต้น...";
			mes " เหมือนกันทุกอย่าง";
			break;
		case 4:
			mes "[ผู้บรรยาย PVP]";
			mes " การเซฟตำแหน่ง...";
			mes " เรียบร้อยแล้ว";
			mes " ขอบคุณมาก";
			mes " ที่ใช้บริการ";
				if(strnpcinfo(4) == "morocc_in") { savepoint "morocc_in",141,139; }
				if(strnpcinfo(4) == "alberta_in") { savepoint "alberta_in",22,148; }
				if(strnpcinfo(4) == "prt_in") { savepoint "prt_in",54,137; }
				if(strnpcinfo(4) == "geffen_in") { savepoint "geffen_in",70,59; }
				if(strnpcinfo(4) == "payon_in01") { savepoint "payon_in01",169,108; }
			break;
		case 5:
			mes "[ผู้บรรยาย PVP]";
			mes "นี่คือสงครามระหว่างผู้เล่น";
			mes "และเกมส์ที่ทำให้สนุก..";
			mes "บรรดาผู้เล่นสนุกกับ...!";
			mes "โหมด PVP อย่างมาก!";
			mes "ขอต้อนรับเจ้า!";
			mes "สู่ความท้าทาย!";
		break;
	}
	close;
}
morocc_in,144,138,4	duplicate(pvpe)	ผู้บรรยาย PVP#pe1	4_M_02
alberta_in,22,146,4	duplicate(pvpe)	ผู้บรรยาย PVP#pe2	4_M_02
prt_in,56,140,4	duplicate(pvpe)	ผู้บรรยาย PVP#pe3	4_M_02
geffen_in,67,63,4	duplicate(pvpe)	ผู้บรรยาย PVP#pe4	4_M_02
payon_in01,173,109,4	duplicate(pvpe)	ผู้บรรยาย PVP#pe5	4_M_02
geffen_in,67,63,4	duplicate(pvpe)	ผู้บรรยาย PVP#pe6	4_M_02

// Mode Select
//============================================================
-	script	Gate Keeper::gkut	-1,{
	mes "[Gate Keeper]";
	mes " ข้ายินดีที่ได้บริการเจ้า";
	mes " ข้ากำลังจะเปิดจตุรัสการต่อสู้่ PVP...";
	mes " 'PVP Gate Keeper'";
	mes " เกี่ยวกับโหมด PVP....";
	mes " หากมีข้อสงสัยใดๆ...";
	mes " โปรดถามผู้บรรยาย";
	next;
	switch(select(" เข้าสู่จตุรัสการต่อสู้ ^FF5533' โหมด PvP Nightmare '^000000"," เข้าสู่จตุรัสการต่อสู้ ^3355FF' โหมด PvP Yoyo '^000000"," เข้าสู่จตุรัสการต่อสู้ ^3355FF' โหมด PvPFighting'^000000"," สิ้นสุดการสนทนา")) {
		case 1:
			mes "[Gate Keeper]";
			mes " โหมด PVP Nightmare...";
			mes " ได้ถูกยกเลิกให้บริการแล้ว";
			mes " หากต้องการที่จะ PVP";
			mes " กรุณาใช้ Yoyo Mode";
			break;
		case 2:
			mes "[Gate Keeper]";
			mes " ค่าใช้จ่ายในการเข้าร่วม 500 Zeny";
			mes " เจ้าต้องการไปยัง...";
			mes " จตุรัสการต่อสู้...";
			mes " โหมด Monkey ไหม?";
			next;
			switch(select(" ไป"," ยกเลิก")) {
			case 1:
				if ((Zeny > 499) && (BaseLevel > 30)) {
					Zeny -= 500;
					callsub L_Warp,"pvp_y_room";
				}
				else {
					mes "[Gate Keeper]";
					mes " นี่เจ้าไม่เข้าใจที่ข้าพูดรึไง!!";
					mes " เจ้ากลับไปตรวจสอบว่าเจ้ามีเงินถึง 500 Zeny และมีเลเวล มากกว่า 30 รึยัง?";
				}
				close;
			case 2:
				mes "[Gate Keeper]";
				mes "นี่คือสงครามระหว่างผู้เล่น";
				mes "และเกมส์ที่ทำให้สนุก..";
				mes "บรรดาผู้เล่นสนุกกับ...!";
				mes "โหมด PVP อย่างมาก!";
				mes "ขอต้อนรับเจ้า!";
				mes "สู่ความท้าทาย!";
				close;
			}
		case 3:
			mes "[Gate Keeper]";
			mes " ยินดีต้อนรับ...";
			mes " กรุณาตรวจสอบดูอีกที่....";
			mes " ว่ามีบัตรเข้าร่วมหรือบัตรชมหรือยัง!";
			next;
			if ((countitem(7028) == 0) && (countitem(7029) == 0)) {
				mes "[Gate Keeper]";
				mes " ...หือ? ยังไม่มีเหรอ?";
				mes " เสียใจด้วยนะ!";
				mes "จตุรัสการต่อสู้นี่";
				mes " สำหรับคนที่มีบัตรเข้าร่วมหรือบัตรชมเท่านั้น...";
				mes " ถึงจะเข้าไปได้";
				close;
			}
			mes "[Gate Keeper]";
			mes " ขอบคุณที่ร่วมสนุกกับกิจกรรมของเรา!";
			if (Zeny >= 500) Zeny -= 500;
			callsub L_Warp,"pvp_room";
		break;
		case 4:
			mes "[Gate Keeper]";
			mes "นี่คือสงครามระหว่างผู้เล่น";
			mes "และเกมส์ที่ทำให้สนุก..";
			mes "บรรดาผู้เล่นสนุกกับ...!";
			mes "โหมด PVP อย่างมาก!";
			mes "ขอต้อนรับเจ้า!";
			mes "สู่ความท้าทาย!";
			break;
	}
	close;

L_Warp:
	set .@map$,getarg(0);
	if (checkre(0)) {
		switch(rand(1,10)) {
			case 1: warp .@map$,34,38; break;
			case 2: warp .@map$,46,38; break;
			case 3: warp .@map$,58,38; break;
			case 4: warp .@map$,70,38; break;
			case 5: warp .@map$,82,38; break;
			case 6: warp .@map$,82,58; break;
			case 7: warp .@map$,65,58; break;
			case 8: warp .@map$,49,58; break;
			case 9: warp .@map$,33,58; break;
			case 10: warp .@map$,52,24; break;
		}
	} else
		warp .@map$,51,23;
	return;
}
morocc_in,144,142,4	duplicate(gkut)	Gate Keeper#gke1	4_M_01
alberta_in,26,146,4	duplicate(gkut)	Gate Keeper#gke2	4_M_01
prt_in,52,140,4	duplicate(gkut)	Gate Keeper#gke3	4_M_01
geffen_in,63,63,4	duplicate(gkut)	Gate Keeper#gke4	4_M_01
payon_in01,175,106,4	duplicate(gkut)	Gate Keeper#gke5	4_M_01

// PVP Warper Function
//============================================================
// Note: The warper NPCs are in the pre-re/re paths.

//callfunc "F_PVP_FSRS"{,<min_lvl>,<max_lvl>};
function	script	F_PVP_FSRS	{
	if (getargcount()) {
		if (BaseLevel < getarg(0) || BaseLevel > getarg(1)) {
			mes "[PVP Fight Square Reception Staff]";
			mes "ขออภัยด้วย, คุณต้องมีเลเวลระหว่าง "+getarg(0)+" ถึง "+getarg(1)+" เท่านั้น";
			close;
		}
	}
	if (strnpcinfo(4) == "pvp_y_room") {
		set .@base$, "pvp_y_"+strnpcinfo(2);
		setarray .@maps$[0],	.@base$+"-1",	.@base$+"-2",	.@base$+"-3",	.@base$+"-4",	.@base$+"-5";
		setarray .@name$[0],	"Prontera",		"Izlude",		"Payon",		"Alberta",		"Morroc";
		setarray .@Limit[0],	128,			128,			128,			128,			128;
	} else {
		setarray .@maps$[0],	"pvp_n_8-1",	"pvp_n_8-2",	"pvp_n_8-3",	"pvp_n_8-4",	"pvp_n_8-5";
		setarray .@name$[0],	"Sandwich",		"Lock on",		"Four Room",	"Under cross",	"Compass Room";
		setarray .@Limit[0],	64,				32,				32,				32,				32;
	}
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		set .@menu$, .@menu$+.@name$[.@i]+" ["+getmapusers(.@maps$[.@i])+" / "+.@Limit[.@i]+"]:";
	set .@menu$, .@menu$+"ยกเลิก";
	set .@i, select(.@menu$)-1;
	if (.@i == 5) close;
	if (getmapusers(.@maps$[.@i]) >= .@Limit[.@i]) {
		mes "[PVP Fight Square Reception Staff]";
		mes " ขอโทษนะ ห้อง PVP นี้เต็มแล้ว ไว้มาใหม่คราวหน้านะ...!";;
		close;
	}
	warp .@maps$[.@i],0,0;
	end;
}



// Event Contestant Entrance
//============================================================
pvp_room,54,85,4	script	Registration Staff#1	8W_SOLDIER,{
	if (countitem(7028) == 0) {
		mes "[PVP Combat Square Register Staff]";
		mes " เอ๊ะ? เจ้ามาทำอะไรที่นี่?";
		mes " นี่คือทางเข้าสำหรับผู้เล่นเพียงคนเดียว";
		mes " สำหรับรายละเอียดของผู้ที่ต้องการรับชม สามารถดำเนินการสมัครสมาชิกทางด้านขวาของคุณ";
		close;
	}
	set .@size, getarraysize(.warp_x);
	if (.@size == 0) {
		setarray .warp_x,40,59,20,40;
		setarray .warp_y,59,40,40,20;
	}
	if (select("Combat Square one:Cancel") == 1) {
		mes "[PVP Combat Square Register Staff]";
		mes "'"+ strcharinfo(0) +"'";
		mes " พร้อมแล้วใช่ไหม?!";
		next;
		if (select(" ตกลง!: ยกเลิก!") == 1) {
			mes "[PVP Combat Square Register Staff]";
			mes " โอเค!, ข้าจะพาเจ้าไปทันที";
			mes " ขอให้โชคดี!";
			close2;
			delitem 7028,1; //Invite_For_Duel
			set .@rand, rand(.@size);
			warp "pvp_2vs2", .warp_x[.@rand], .warp_y[.@rand];
			deletearray .warp_x[.@rand],1;
			deletearray .warp_y[.@rand],1;
			end;
		}
		mes "[PVP Combat Square Register Staff]";
		mes " แล้วกลับมาใหม่เมื่อเจ้าพร้อม.";
		close;
	}
	close;

OnInit:
	waitingroom "ทางเข้าสำหรับจตุรัสการต่อสู้",0;
	end;
}

// Spectator Entrance
//============================================================
-	script	Spectator's Entrance#dum::PVPSpectator	-1,{
	if (countitem(7029) == 0) {
		mes "[PVP Compete Square Register Staff]";
		mes " นี่คือประตูทางเข้าสำหรับผู้รับชม";
		mes " สำหรับรายละเอียดการเข้าเล่น สามารถดำเนินการสมัครสมาชิกได้ทางซ้ายมือของท่าน";
		close;
	}
	if (select(" เข้าสู่จตุรัสการต่อสู้: ยกเลิก") == 1) {
		mes "[PVP Combat Square Register Staff]";
		mes " คุณได้เข้าร่วมการต่อสู้, ขอให้สนุกกับกิจกรรม!";
		close2;
		delitem 7029,1; //Admission_For_Duel
		switch(rand(1,4)) {
			case 1: warp "pvp_2vs2",39,7; end;
			case 2: warp "pvp_2vs2",39,73; end;
			case 3: warp "pvp_2vs2",7,39; end;
			case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	close;

OnInit:
	waitingroom "เข้าสู่การรับชมจตุรัสการต่อสู้",0;
	end;
}
pvp_room,62,85,4	duplicate(PVPSpectator)	Spectator's Entrance#1	8W_SOLDIER
pvp_room,70,85,4	duplicate(PVPSpectator)	Spectator's Entrance#2	8W_SOLDIER
pvp_room,78,85,4	duplicate(PVPSpectator)	Spectator's Entrance#3	8W_SOLDIER
pvp_room,86,85,4	duplicate(PVPSpectator)	Spectator's Entrance#4	8W_SOLDIER

// Spectator Warps
//============================================================
-	script	Combat Square Staff#dum::PVPSpecWarp	-1,{
OnTouch:
	mes "[Combat Square Staff]";
	mes " มีอะไรให้ช่วยไหม?";
	if (select("เข้าสู่ใจกลางการต่อสู้:ออกจากจตุรัสการต่อสู้") == 1) {
		warp "pvp_2vs2",38,38;
		end;
	}
	warp "pvp_room",84,39;
	end;
}
pvp_2vs2,5,4,0	duplicate(PVPSpecWarp)	Combat Square Staff#1	WARPNPC,1,1
pvp_2vs2,5,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#2	WARPNPC,1,1
pvp_2vs2,74,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#3	WARPNPC,1,1
pvp_2vs2,74,5,0	duplicate(PVPSpecWarp)	Combat Square Staff#4	WARPNPC,1,1

pvp_2vs2,40,40,0	script	Combat Square Staff#5	WARPNPC,1,1,{
        mes "[Combat Square Staff]";
	mes " มีอะไรให้ช่วยไหม?";
        switch(select("เข้าสู่ใจกลางการต่อสู้:ออกจากจตุรัสการต่อสู")) {
        case 1:
                switch(rand(1,4)) {
                        case 1: warp "pvp_2vs2",39,7; end;
                        case 2: warp "pvp_2vs2",39,73; end;
                        case 3: warp "pvp_2vs2",7,39; end;
                        case 4: warp "pvp_2vs2",73,39; end;
                }
        case 2:
                warp "pvp_c_room",84,39;
                end;
        }
}

// PVP Area Exit warp
//============================================================
pvp_room,51,19,0	script	out#eventpvp	WARPNPC,4,2,{
OnTouch:
	mes "[Combat Square Staff]";
	mes "ท่านรู้สึกอย่างไรบ้างกับการต่อสู้?";
	mes "เลือกสถานที่ที่คุณต้องการจะไป?";
	next;
	switch(select("Prontera.:Morocc.:Geffen.:Payon.:Alberta.:ยกเลิก")) {
		case 1: warp "prontera",107,60; end;
		case 2: warp "morocc",157,96; end;
		case 3: warp "geffen",120,36; end;
		case 4: warp "payon",90,113; end;
		case 5: warp "alberta",41,243; end;
		case 6: close;
	}
}
