function	script	F_ClearJobVar	{
	set JBLVL,0;
	set FIRSTAID,0;
	set PLAYDEAD,0;
	set got_bandage,0;
	set got_novnametag,0;
	set job_acolyte_q,0; set job_acolyte_q2,0;
	set job_archer_q,0;
	set job_magician_q,0;
	set job_merchant_q,0; set job_merchant_q2,0; set job_merchant_q3,0;
	set job_sword_q,0; set SWTEST, 0;
	set job_thief_q,0;
	set SUPNOV_Q,0;
	set ASSIN_Q,0; set ASSIN_Q2,0; set ASSIN_Q3,0;
	set BSMITH_Q,0; set BSMITH_Q2,0;
	set HNTR_Q,0; set HNTR_Q2,0;
	set KNIGHT_Q,0; set KNIGHT_Q2,0;
	set PRIEST_Q,0; set PRIEST_Q2,0; set PRIEST_Q3,0;
	set WIZ_Q,0; set WIZ_Q2,0;
	set ROGUE_Q,0; set ROGUE_Q2,0;
	set ALCH_Q,0; set ALCH_Q2,0;
	set CRUS_Q,0;
	set MONK_Q,0; set JOB_MONK_C,0;
	set SAGE_Q,0; set SAGE_Q2,0;
	set DANC_Q,0;
	set BARD_Q,0;
	set TAEK_Q,0; set TK_Q,0;
	set STGL_Q,0;
	set SOUL_Q,0;
	set GUNS_Q,0;
	set NINJ_Q,0;
	return;
}

function	script	F_ClearGarbage	{
	if(TURTLE==20) set MISC_QUEST,MISC_QUEST | 65536;
	if(MISC_QUEST & 65536) set TURTLE,0; //now clear the var if the quest is over
	set ADV_QSK,0;
	set ADV_QSK2,0;
	set RES_SKILL,0;
	set wizard_m2,0;
	set NEW_MES_FLAG0,0;
	set NEW_MES_FLAG1,0;
	set NEW_MES_FLAG2,0;
	set NEW_MES_FLAG3,0;
	set NEW_MES_FLAG4,0;
	set NEW_MES_FLAG5,0;
	set NEW_LVUP0,0;
	set NEW_LVUP1,0;
	set NEW_JOBLVUP,0;
	set dtseligible,0;
	set MISC_QUEST,MISC_QUEST & ~128;
	
	return;
}

function	script	Job_Change	{
	jobchange getarg(0),Upper;
	logmes "CLASS CHANGE: " +strcharinfo(0)+ " become a "+jobname(Class);
	return;
}

function	script	F_RandMes	{
	return getarg(rand(1,getarg(0)));
}

function	script	F_SexMes	{
	return getarg(Sex);
}

function	script	F_Hi	{
	return callfunc("F_RandMes",5,"Hi!","Hello!","Good day!","How are you?","Hello there.");
}

function	script	F_Bye	{
	return callfunc("F_RandMes",6,"Bye. See you again.","Later.","Goodbye.","Good luck!","Have a nice day!","Byebye!!!");
}

function	script	F_ItemName	{
	set @t$,"^000090";
	if(getarg(3)) set @t$,@t$+"+"+getarg(3)+" ";
	if(getarg(2)==1) set @t$,@t$+"VS ";
	if(getarg(2)==2) set @t$,@t$+"VVS ";
	if(getarg(2)==3) set @t$,@t$+"VVVS ";
	if(getarg(2)>3) set @t$,@t$+getarg(2)+"xVS ";
	if(getarg(1)==1) set @t$,@t$+"Ice ";
	if(getarg(1)==2) set @t$,@t$+"Earth ";
	if(getarg(1)==3) set @t$,@t$+"Fire ";
	if(getarg(1)==4) set @t$,@t$+"Wind ";
	if(getarg(1)>4) set @t$,@t$+"Strange ";
	return @t$+getitemname(getarg(0))+"^000000";
}

function	script	getJobName	{
	return jobname(getarg(0));
}

function	script	F_SaveQuestSkills	{
	set ADV_QSK,0; set ADV_QSK2,0;
	for(set @i, 0; @i < 14; set @i, @i + 1){
	if(getskilllv(144+@i)) set ADV_QSK,ADV_QSK|pow(2,@i);
	}
	for(set @i, 0; @i < 19; set @i, @i + 1){
	if(getskilllv(1001+@i)) set ADV_QSK2,ADV_QSK2|pow(2,@i);
	}
	return;
}

function	script	F_Load1Skills	{
	for(set @i, 0; @i < 14; set @i, @i + 1){
	if(ADV_QSK|pow(2,@i) == ADV_QSK) skill 144+@i,1,0;
	}
	set ADV_QSK,0; //Clear var
	return;
}

function	script	F_Load2Skills	{
	for(set @i, 0; @i < 19; set @i, @i + 1){
	if(ADV_QSK2|pow(2,@i) == ADV_QSK2) skill 1001+@i,1,0;
	}
	set ADV_QSK2,0; //Clear var
	return;
}

function	script	F_GetWeaponType	{
	switch(getiteminfo(getarg(0),11)) {
	case 1: return "Dagger"; break;
	case 2: return "One-handed Sword"; break;
	case 3: return "Two-handed Sword"; break;
	case 4: return "One-handed Spear"; break;
	case 5: return "Two-handed Spear"; break;
	case 6: return "One-handed Axe"; break;
	case 7: return "Two-handed Axe"; break;
	case 8: return "Mace"; break;
	//case 9: return "Unused"; break;
	case 10: return "Staff"; break;
	case 11: return "Bow"; break;
	case 12: return "Knuckle"; break;
	case 13: return "Instrument"; break;
	case 14: return "Whip"; break;
	case 15: return "Book"; break;
	case 16: return "Katar"; break;
	case 17: return "Revolver"; break;
	case 18: return "Rifle"; break;
	case 19: return "Gatling gun"; break;
	case 20: return "Shotgun"; break;
	case 21: return "Grenade Launcher"; break;
	case 22: return "Shuriken"; break;
	default: return "Unable to Determine Equip Type"; break;
	}
	end;
}

function	script	F_GetArmorType	{
	switch(getiteminfo(getarg(0),5)) {
	case 001: return "Lower Headgear"; break;
	case 2: return "Weapon"; break;
	case 4: return "Garment"; break;
	case 8: return "Accessory"; break;
	case 16: return "Armor"; break;
	case 32: return "Shield"; break;
	case 64: return "Shoes"; break;
	case 128: return "Accessory"; break;
	case 256: return "Upper Headgear"; break;
	case 512: return "Middle Headgear"; break;
	default: return "Unable to Determine Equip Location"; break;
	}
	end;
}

function	script	Time2Str	{
	set .@Time_Left, getarg(0) - gettimetick(2);
	set .@Days, .@Time_Left / 86400;
	set .@Time_Left, .@Time_Left - (.@Days * 86400);
	set .@Hours, .@Time_Left / 3600;
	set .@Time_Left, .@Time_Left - (.@Hours * 3600);
	set .@Minutes, .@Time_Left / 60;
	set .@Time_Left, .@Time_Left - (.@Minutes * 60);
	set .@Time$, "";
	if( .@Days > 1 )
	set .@Time$, .@Time$ + .@Days + " days, ";
	else if( .@Days > 0 )
	set .@Time$, .@Time$ + .@Days + " day, ";
	if( .@Hours > 1 )
	set .@Time$, .@Time$ + .@Hours + " hours, ";
	else if( .@Hours > 0 )
	set .@Time$, .@Time$ + .@Hours + " hour, ";
	if( .@Minutes > 1 )
	set .@Time$, .@Time$ + .@Minutes + " minutes, ";
	else if( .@Minutes > 0 )
	set .@Time$, .@Time$ + .@Minutes + " minute, ";
	if( .@Time_Left > 1 || .@Time_Left == 0 )
	set .@Time$, .@Time$ + .@Time_Left + " seconds";
	else if( .@Time_Left == 1 )
	set .@Time$, .@Time$ + .@Time_Left + " second";
	return .@Time$;
}
