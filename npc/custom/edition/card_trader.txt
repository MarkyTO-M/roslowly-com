//===== rAthena Script =======================================
//= Card Trader
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1 
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Exchange cards for points.
//============================================================
 
morocc,135,110,4	script	Card Trader	419,{
	
	mes "[^995050Card Trader^000000]";
	mes "Hi, "+strcharinfo(0)+"!";
	mes "คุณต้องการทำอะไรหรอ ?";
	next;
	switch(select(" > ขายการ์ด: > แลกคะแนน shop (^0055FF"+getd(.Points$)+"^000000): > ออก")) {
	case 1:
		mes "[^995050Card Trader^000000]";
		mes "กรุณาเลือกไอเทมที่ต้องการขาย";
		if (.Level) {
			mes " ";
			mes "การ์ดนี้ต้องดรอปจาก มอนเตอร์ที่มีเลเวล" + .Level+"ขึ้นไป";
		}
		deletearray @sold_nameid[0],getarraysize(@sold_nameid);
		callshop "card_trader_shop",2;
		npcshopattach "card_trader_shop";
		end;
	case 2:
		mes "[^995050Card Trader^000000]";
		mes "You have ^0055FF"+getd(.Points$)+"^000000 Point"+((getd(.Points$) == 1)?".":"s.");
		callshop "card_trader_shop",1;
		npcshopattach "card_trader_shop";
		end;
	case 3:
		mes "[^995050Card Trader^000000]";
		mes "*yawn*";
		mes "แล้วพบกันใหม่";
		emotion e_yawn;
		close;		
	}
end;
OnSellItem:
	mes "Cards to sell:";
	mes "-----------------------------------";
	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)
		if (@sold_nameid[.@i] > 4000 && @sold_nameid[.@i] < 4700) {
			if (.Level) {
				query_sql("SELECT `LV` FROM `mob_db` WHERE `DropCardid` = "+@sold_nameid[.@i],.@lv);
				if (.@lv < .Level) {
					dispbottom getitemname(@sold_nameid[.@i])+" is under the minimum level.";
					continue;
				}
			}
			set .@card_id[getarraysize(.@card_id)], @sold_nameid[.@i];
			set .@card_amt[getarraysize(.@card_amt)], @sold_quantity[.@i];
			set .@class2, compare(.class2$,""+@sold_nameid[.@i]);
			set .@class3, compare(.class3$,""+@sold_nameid[.@i]);
			set .@class4, compare(.class4$,""+@sold_nameid[.@i]);
			mes ((.@class2 || .@class3 || .@class4)?"  ^FF0000":"  ^777777")+@sold_quantity[.@i]+"x "+getitemname(@sold_nameid[.@i])+"^000000";
			if(.@class2) { .@class=1; }
			else if(.@class3) { .@class=2; }
			else if(.@class4) { .@class=3; }
			else { .@class=0; } 
			set .@card_total, .@card_total+ ( @sold_quantity[.@i]* .Points[.@class] );
		}
	deletearray @sold_nameid[0], getarraysize(@sold_nameid);
	deletearray @sold_quantity[0], getarraysize(@sold_quantity);
	if (!.@card_id) {
		mes "  ^777777(none)^000000";
		emotion e_swt;
		close;
	}
	mes " ";
	mes "---------- รวม : ^0055FF"+.@card_total+" pt.^000000 -------";
	next;
	if(select(" > ^0055FFตกลง^000000: > ^777777ยกเลิก^000000") == 2) {
		mes "[^995050Card Trader^000000]";
		mes "Oh, okay...";
		emotion e_hmm;
		close;
	}
	for(set .@i,0; .@i<getarraysize(.@card_id); set .@i,.@i+1)
		delitem .@card_id[.@i],.@card_amt[.@i];
	setd .Points$, getd(.Points$)+.@card_total;
	mes "[^995050Card Trader^000000]";
	mes "เรียบร้อย";
	emotion e_ho;
	close;
end;
OnBuyItem:
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	if (.@cost > getd(.Points$)) {
		mes "[Card Trader]";
		mes "คุณมีคะแนนไม่เพียงพอ";
		emotion e_omg;
	}
	else {
		if(.@cost <= 0){
			end;
		}
		mes "Items purchased:";
		mes "-----------------------------------";
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			mes "  ^777777"+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+"^000000";
		}
		mes " ";
		mes "---------- รวม : ^0055FF"+.@cost+" pt.^000000 -------";
		setd .Points$, getd(.Points$)-.@cost;
		emotion e_cash;
	}
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;
end;
OnInit:
	waitingroom "รับซื้อ การ์ดขยะ ",0;
	set .Level,0;		   // Minimum monster level to trade corresponding cards.
	set .Points$,"#Card_Points";	// Variable to store points.
	// Card Shop items: <ID>,<point cost>
	setarray .Shop[0], 7647,4,677,6,616,6,607,4,12210,12,7619,100,7620,100;
	
	setarray .Points[0],1,1,2,2;	// Points per <Class 1>,<Class 2>,<Class 3>,<Class 4>
	set .class1$,"501,502";
	
	// Class 2 cards | Those not within Class 2 ~4 are all Class 1
	set .class2$,"503,504";
		
	set .class3$,"505,506";
		
	// List of MVP cards. | Previously .MVP$
	set .class4$,"507,508";
	
	npcshopdelitem "card_trader_shop",909;
	// for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2){npcshopadditem "card_trader_shop",.Shop[.@i],.Shop[.@i+1];}
	end;
}
-	shop	card_trader_shop	-1,7647:4,677:6,616:6,607:4,12210:12,7619:100,7620:100