//=============================================================
//			-             mOnster studiO Scripts               -
//				www.monsterstudio.in.th
//#############################################################
//12548 Shabby_Purchase_Street
// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================ 
morocc,132,62,4	script	buyingskill::Buyskill	1_F_ORIENT_04,{
	mes "[ยายมา]";
	if (BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if (getskilllv("ALL_BUYING_STORE") == 1) {
			mes "ข้าเป็นทีมงานจัดซื้อ";
			mes "วันนี้จะให้ช่วยอะไรละ";
			next;
			if(select("ซื้อ Bulk Buyer Shop:ไม่มีอะไร") == 2) {
				mes "โปรดถามข้า หากท่านต้องการใบอนุญาตที่เป็นกลุ่มร้านค้าผู้ซื้อ.";
				mes "แล้วมาใหม่นะ~";
				end;
			}
			mes " Bulk Buyer Shop License ราคาใบละ 200 zeny สามารถซื้อได้สูงสุด 50 ต่อครั้ง.";
			mes "ท่านต้องการเท่าไหร่ละ";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "ท่านยกเลิกการซื้อขาย";
					mes "ขอให้โชคดี";
					end;
				} else if (.@input > 50) {
					mes "โปรดใส่จำนวนที่ต้องการ 50 หรือน้อยกว่านั้น";
					next;
				} else {
					mes "ท่านซื้อ"+(.@input*200)+" zeny สำหรับ "+.@input+" licenses.";
					if(Zeny < (.@input*200)) {
						mes "ดูคล้ายท่านจะมีเงินไม่เพียงพอ";
					} else {
						mes "ขอบคุณหลาย ๆ.";
						getitem 6377,.@input; //Buy_Stall_Permit
						set Zeny,Zeny-(.@input*200);
					}
					end;
				}
			}
		} else {
			mes "ข้าคือหัวหน้าทีม จัดซื้อ.";
			mes "ท่านเป็น...พ่อค้าใช่ไหม";
			next;
			mes "ท่านรู้ไหม? ว่าสมาคมของพวกเรานำ ใบอนุญาติออกมาเพื่ออนุญาติ ในเพื่อซื้อสินค้าจากผู้อื่น. ";
			next;
			select("ข้าไม่เคยเรื่องกำลังของการซื้อ...");
			mes "ถูกต้อง แต่คิดให้ดี:";
			mes "ท่านไม่มีเวลามาก ในการรับซื้อสินค้า";
			mes "ไม่มีเวลาพบปะพูดคุย";
			mes "ไม่สะดวกและลำบากใช่ไหม";
			next;
			mes "มาลงทะเบียนกับเราใช้เงิน 10,000 zenyจ่ายครั้งเดียวเท่านั้น";
			mes "แล้วท่าจะได้ ^4A4AFFBulk Buyer Shop License^000000 ไว้ตั้งร้านรับซื้อแทนท่าน";
			next;
			mes "ต้องการลงทะเบียนเลยไหม";
			mes "ถ้าตกลงข้าจะสอนท่านใช้ Bulk Buyer Shop.";
			next;
			if(select("เรียนรู้ Bulk Buyer Shop:ยกเลิกดีกว่า") == 2) {
				mes "น่าเสียดายนะ!";
				mes "โอกาสมาถึงแล้วแท้ๆ";
				mes "ถ้าสนใจกลับมาคุยกับข้าล่ะ";
				end;
			}
			if (Zeny < 10000) {
				mes "ค่าธรรมเนียมการลงทะเบียนเป็น. 10,000 zeny.";
				mes "โปรดมีค่าธรรมเนียมเตรียมพร้อม";
				end;
			} else {
				mes "ท่านตัดสินใจได้ดี";
				mes "ลงชื่อของท่านและ";
				mes "โปรดให้ค่าธรรมเนียม การลงทะเบียนแก่ข้า";
				next;
				input .@name$;
				mes .@name$+"....";
				mes "ข้าชอบลายมือท่าน.";
				mes "โอเคท่านจะได้รับ Bulk Buyer Shop.";
				set Zeny,Zeny-10000;
				getitem 6377,5; //Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,SKILL_PERM_GRANT;
				next;
				mes "เวลาจะไปตั้งร้านซื้อ ท่านต้องมีไอเทมที่จะรับซื้อ ในตัวท่านก่อนเพื่อเป็นตัวอย่าง";
				next;
				mes "ปรับตารางสกิลให้เป็นแบบเล็ก ดูในช่อง Misc จะเห็นสกิลตั้งร้านรับซื้อ กดใช้มันได้เลย";
				next;
				mes "ข้ามอบ Bulk Buyer Shop Licenses 5 ชิ้นแก่ท่านนำไปลองใช้ดูนะ";
				mes "มีความสุขกับการค้าขายนะ";
				end;
			}
		}
	}
	mes "ข้าเป็นทีมจัดซื้อ ของสมาคมค้าขาย.";
	mes "ข้าไม่มีอะไรจะคุยกับท่าน";
	next;
	mes "เวลาของข้าถูกอุทิศให้ลูกค้าของพวกเราในอุตสาหกรรมพาณิชย์เพียงผู้เดียว.";
	end;	
	OnInit:
	waitingroom "Buyer Shop Skill",0;
	end;
}