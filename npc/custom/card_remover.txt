//===== rAthena สคริป =======================================
//= NPC ถอด การ์ด
//===== โดย: ==================================================
//= TyrNemesis^
//=====  แปลไทย โดย: ==================================================
//= Rirakkuma Sergeant Bearing
//===== เวอร์ชั่น ปัจจุบัน: =====================================
//= 1.2a
//===== ใช้กับ: =====================================
//= โปรเจค โครงการ rAthena
//===== คำอธิบาย: =========================================
//= นำการ์ดออกจากไอเท็มที่ได้ทำการติดตั้ง.
//===== ความเหนเพิ่มเติม: =================================
//= 1.0 เวอร์ชั่น แรก. [TyrNemesis^]
//= 1.2 เพิ่มประสิทธิภาพ และ  getequipname เมนู. [Kisuka]
//= 1.2a เพิ่มคำสั่ง 'disable_items' [Euphy]
//============================================================

morocc,129,59,4	script	พนักงานถอดการ์ด#eAcustom	1_F_ORIENT_04,{

	disable_items;
	mes "[ถอดการ์ด]";
	mes "คุณต้องการจะลบล้างพลังใช่ไหม.";
	mes "มันอันตรายมาก มีโอกาสสำเร็จ 90% เท่านั้น !!";
	mes "หากไม่สำเร็จ";
	mes "ไอเทมของท่านจะหายไปทันที";
	mes "* ค่าใช้จ่าย 100,000 Zeny";
	next;
	
	if(select("ตกลง:ยกเลิก") == 2) {
		mes "[ถอดการ์ด]";
		mes "...";
		end;
	}
	
	if (checkweight(1201,1) == 0) {
		mes "คุณมีไอเทมมากเกินไป";
		end;
	}
	if (MaxWeight - Weight < 500) {
		mes "กรุณาตรวจสอบน้ำหนักด้วย";
		end;
	}

	setarray .@position$[1],"Accessory 1","Accessory 2","Left Hand","Right Hand","Armor","Footgear","Garment","H Top","H Mid","H Low";
	setarray .@indices[1], EQI_ACC_L, EQI_ACC_R,EQI_HAND_L,EQI_HAND_R,EQI_ARMOR,EQI_SHOES,EQI_GARMENT,EQI_HEAD_TOP,EQI_HEAD_MID,EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@indices[.@i]))? .@position$[.@i]+" ["+getequipname(.@indices[.@i])+"]":.@position$[.@i]+" []")+":";
	set .@sel_part, .@indices[ select(.@menu$) ];
	
	set .@part,getequipid(.@sel_part);
	set .@part_del,.@sel_part;
	set .@equip_refine, getequiprefinerycnt(.@part_del);
	setarray .@equip_card[0], getequipcardid(.@part_del,0), getequipcardid(.@part_del,1), getequipcardid(.@part_del,2), getequipcardid(.@part_del,3);
	
	if (!getequipisequiped(.@sel_part)) {
		mes "[ถอดการ์ด]";
		mes "คุณไม่ได้ใส่ไอเท็ม?";
		end;
	}
	
	if (.@equip_card[0] == 0 && .@equip_card[1] == 0 && .@equip_card[2] == 0 && .@equip_card[3] == 0) {
		mes "[ถอดการ์ด]";
		mes "ไอเท็มนี้ไม่สามารถลบได้ กรุณาตรวจสอบดูอีกครั้ง";
		end;
	}
	
	if(Zeny < 100000){
		mes "...เงินคุณไม่พอ 100,000 Zeny";
		end;
	}
	
	if(select("ยืนยันอีกครัง:ยกเลิกทั้งหมด") == 2) {
		mes "[ถอดการ์ด]";
		mes "...ไว้กลับมาใหม่นะ";
		end;
	}
	
	Zeny = Zeny-100000;
	
	delequip .@part_del;
	
	set .@successful_rand,rand(100);
	set .@successful,0;
	
	if(.@successful_rand <= 90){set .@successful,1;}
	
	if(.@successful == 0 ){
		mes "[ถอดการ์ด]";
		specialeffect2 306;
		mes "ฉันเสียใจด้วยจริงๆ ";
		mes "^FF0000 มันล้มเหลว...^000000";
		mes "ไอเทมของคุณหายไปแล้ว !!";
		end;
	}
	
	specialeffect2 305;
	
	mes "[ถอดการ์ด]";
	mes "ทำการล้างให้เรียบร้อย.";
	
	set .@rune_2,.@equip_card[2];
	set .@rune_1,.@equip_card[3];
	
	if(.@rune_2 >= 4700 && .@rune_2 <= 4999){
		if(.@equip_card[0] != 0){
			getitem .@equip_card[0],1;
		}
		if(.@equip_card[1] != 0){
			getitem .@equip_card[1],1;
		}
		getitem2 .@part,1,1,.@equip_refine,0,0,0,.@rune_2,.@rune_1;
		end;
	}else{
		if(.@rune_1 >= 4700 && .@rune_1 <= 4999){
			if(.@equip_card[0] != 0){
				getitem .@equip_card[0],1;
			}
			if(.@equip_card[1] != 0){
				getitem .@equip_card[1],1;
			}
			if(.@equip_card[2] != 0){
				getitem .@equip_card[2],1;
			}
			getitem2 .@part,1,1,.@equip_refine,0,0,0,0,.@rune_1;
			end;
		}else{
			if(.@equip_card[0] != 0){
				getitem .@equip_card[0],1;
			}
			if(.@equip_card[1] != 0){
				getitem .@equip_card[1],1;
			}
			if(.@equip_card[2] != 0){
				getitem .@equip_card[2],1;
			}
			if(.@equip_card[3] != 0){
				getitem .@equip_card[3],1;
			}
			getitem2 .@part,1,1,.@equip_refine,0,0,0,0,0;
			end;
		}
	}
	
	end;
	OnInit: 
	waitingroom "บริการถอดการ์ด",0; 
	end;
	
}
