//===== rAthena สคริป =======================================
//= NPC ถอด การ์ด
//===== โดย: ==================================================
//= TyrNemesis^
//=====  แปลไทย โดย: ==================================================
//= Rirakkuma Sergeant Bearing
//===== เวอร์ชั่น ปัจจุบัน: =====================================
//= 1.2a
//===== ใช้กับ: =====================================
//= โปรเจค โครงการ rAthena
//===== คำอธิบาย: =========================================
//= นำการ์ดออกจากไอเท็มที่ได้ทำการติดตั้ง.
//===== ความเหนเพิ่มเติม: =================================
//= 1.0 เวอร์ชั่น แรก. [TyrNemesis^]
//= 1.2 เพิ่มประสิทธิภาพ และ  getequipname เมนู. [Kisuka]
//= 1.2a เพิ่มคำสั่ง 'disable_items' [Euphy]
//============================================================

morocc,129,59,4	script	พนักงานถอดการ์ด#eAcustom	1_F_ORIENT_04,{

	set .zenycost,100000;    // ค่าใช้จ่ายในการให้บริการถอดการ์ด ( Zeny)
	set .percardcost,25000;  // ค่าใช้จ่ายต่อ การ์ด ของการให้บริการถอดการ์ด (Zeny)
	set .faildestroy,1;      // ควรตั้งค่า การถอดการ์ด ให้มีโอกาศของความล้มเหลวในการถอดการ์ด หรือไม่ ? (1=ใช่, 0=ไม่)

	disable_items;
	mes "[Wise Old Woman]";
	mes "หนึ่งวันที่ดี, ฉันมีอำนาจที่จะทำการภอดการ์ดที่คุณได้ประกอบบนอุปกรณ์ไอเท็มของคุณ ?";
	next;
	switch(select("ใช่, ฉันต้องการ.:ค่าใช้บริการ ของคุณคือ?:ไม่ ขอบคุณ.")) {
	case 1:
		mes "[Wise Old Woman]";
		mes "ดีมาก.รายการไอเท็มซึ่งฉันจะต้องตรวจสอบให้คุณ ";
		next;

		setarray .@position$[1], "Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
		set .@menu$,"";
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
		{
			if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i]+" [" + getequipname(.@i) + "]";

			set .@menu$, .@menu$ + ":";
		}
		set .@part,select(.@menu$);
		if(!getequipisequiped(.@part)) {
			mes "[Wise Old Woman]";
			mes "ดูเหมือนคุณไม่ได้สวมใส่อุปกรณ์ที่มีการ์ดอยู่ แล้วฉันควรจะทำอย่างไรดี ?.";
			end;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Wise Old Woman]";
			mes "ไม่มีการ์ดที่ถูกประกอบในรายการไอเท็ม ฉันไม่สามารถทำอะไรกับมัน, ฉันไม่ค่อยชอบใจสักเท่าไร.";
			end;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFรอสักครู่!";
			mes "ฉันไม่สามารถให้บริการ ใดๆ ของฉันให้กับคุณได้เพราะ";
			mes "คุณกำลังแบกน้ำหนักของคุณมากเกินไป";
			mes "นำสิ่งที่คุณคิดว่าไม่น่าจะเป็นรายการพิเศษของคุณ";
			mes "นำไปจัดเก็บ กับ คาฟร่า และ กลับมาอีกครั้ง~";
			end;
		}
		mes "[Wise Old Woman]";
		mes "ก่อนที่ฉันจะเริ่มต้น ฉันต้องเตือนคุณ " + .@cardcount + " ฉันอาจจะล้มเหลว ถ้า ฉันทำพลาด อาจจะทำลายการ์ด หรือ รายการไอเท็ม หรือ ทั้งสองอย่าง " + (.zenycost+(.@cardcount * .percardcost)) + " zeny";
		next;
		if(select("ดีมาก. ทำมันเลย.:ไม่เป็นไร.") == 2) {
			mes "[Wise Old Woman]";
			mes "ดีมาก. และกลับมาอีกครั้งถ้าเมื่อไรคุณต้องการใช้บริการ.";
			end;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost)))) {
			mes "[Wise Old Woman]";
			mes "คุณมีไอเทม หรือ เงินไม่เพียงพอ.";
			end;
		}
		mes "[Wise Old Woman]";
		mes "ก่อนที่ผมจะเริ่มต้น ผมต้องเตือนคุณ,";
		mes "ว่าฉันอาจจะล้มเหลว. ถ้าผมทำ, อาจจะทำลายการ์ด, และ ไอเท็มไปด้วย, ";
		mes "หรือ ทั้งสองอย่าง. และ ฉันจะไม่คืนเงินให้ตามที่กล่าวไว้. ";
		mes "ซึ่งมันเป็นสิ่งสำคัญสำหรับคุณ ";
		mes "ต้องการที่จะรักษา การ์ด, หรือ ไอเท็ม ?";
		next;
		switch(select("ฉันเปลี่ยนใจเกี่ยวกับเรื่องนี้.:เก็บไอเท็ม.:เก็บการ์ด.")) {
		case 1:
			mes "[Wise Old Woman]";
			mes "ดีมาก. และกลับมาอีกครั้งถ้าเมื่อไรคุณต้องการใช้บริการ.";
			end;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Wise Old Woman]";
		mes "ดีมาก. ฉันจะเริ่มต้นเดียวนี้.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		
		// เปลี่ยนค่าคงที่ในอีก 3 ค่า ที่มีค่ามีโอกาศล้มเหลวที่กำหนดไว้ใน refine_db.txt
		// ค่าแรก = โอกาศล้มเหลวทั้งหมด (การ์ด และ ไอเท็ม จะถูกทำลาย)
		// ค่าสอง = มีโอกาศล้มเหลวบางส่วน (สิ่งหนึ่งหรืออื่นๆ จะถูกทำลาย,ตามที่ผู้เล่นกำหนดที่จะรักษาไอเท็มหรือการ์ดไว้)
		// ค่าสาม = มีโอกาศล้มเหลวแต่ไอเท็มและการ์ดไม่เสียหาย (แต่ทั้งหมดที่หายไปคือ ค่าบริการ และ การลงทุน ทั้งหมด)
		/*
		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Wise Old Woman]";
				mes "ในขณะที่ฉันกำลังจัดการเพื่อเอาการ์ดออกจากรายการนั้น. กระบวนการเป็นความล้มเหลวทั้งหมด การ์ด และ ไอเท็ม ได้ถูกทำลาย.";
				end;
			}

			if(.@failchance < 3) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Wise Old Woman]";
					mes "ในขณะที่ฉันกำลังจัดการเพื่อเอาการ์ดออกจากรายการนั้น, การ์ดได้เกิดความล้มเหลวในกระบวนการ การนำออก. แต่ไอเท็ม, ยังคงไม่เป็นอะไร....";
					end;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Wise Old Woman]";
					mes "ส่วนใหญ่ที่จะโชคร้ายที่ผมประสบความสำเร็จในการนำ การ์ดออกจากไอเท็ม. แต่ ไอเท็มนั้นได้ถูกทำลายระหว่างการดำเนินการ, ";
					end;
				}
			}
		}

		if(.@failchance < 5) {
			next;
			failedremovecards .@part,3;
			mes "[Wise Old Woman]";
			mes "จากกระบวนการที่ทำการถอดการ์ดนั้นไม่ประสบผลสำเร็จ. แต่ยังโชคดี, ที่ทั้งสองรายการ ไอเท็ม และ การ์ด ยังคงอยู่สภาพเดิม.";
			end;
		}
		*/
		next;
		successremovecards .@part;
		mes "[Wise Old Woman]";
		mes "กระบวนการ การถอดการ์ดประสบผลสำเร็จ. และ ไอเท็ม และ การ์ด ไม่ถูกทำลายทั้งสองอย่าง โชคดี.";
		end;
	case 2:
		mes "[Wise Old Woman]";
		mes "ผมคิดค่าบริการ "+callfunc("F_InsertComma",.zenycost)+" zeny, บวก "+callfunc("F_InsertComma",.percardcost)+" zeny (ต่อ การ์ด 1 ใบ)";
		end;
	case 3:
		mes "[Wise Old Woman]";
		mes "ดีมาก. และกลับมาอีกครั้งถ้าเมื่อไรคุณต้องการใช้บริการ.";
		end;
	}
	OnInit: 
	waitingroom "บริการถอดการ์ด",0; 
	end;
}
