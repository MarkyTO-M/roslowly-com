//===== Hercules Script ======================================
//= Milk Trader
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Trades bottles for milk
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//============================================================

prontera,73,140,0	script	Milk Vendor	4_M_04,{
	if (checkweight(1201,1) == 0) {
		mes "^3355 นำหนักของท่านเกินกำหนด";
		close;
	}
	mes "[คนขายนม]";
	mes " ถ้าท่านนำ Empty Bottle และเงิน 15 zeny มาให้ข้า ขจะเปลี่ยนเป็น Milk ให้ท่าน 1ขวด!";
	next;
	if (select("Exchange all empty bottles.:Cancel") == 1) {
		if (countitem(713) <= 0) {
			mes "[คนขายนม]";
			mes " โปรดนำ Empty Bottles มาให้ข้า! Empty Bottles ที่แปลว่าขวดเปล่าน่ำ";
			mes " ...ไหนล่ะขวด ...?";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (Zeny < .@total_cost) {
			mes "[คนขายนม]";
			mes " โอ้ว ว๊าาา~!";
			mes " ท่านมีเงินไม่พอ ที่จะแลกเปลี่ยนกับข้า";
			mes " ค่าบริการทั้งหมด " + .@total_cost + " เซนี";
			close;
		}
		if (MaxWeight - Weight < .@total_weight) {
			mes "[คนขายนม]";
			mes " หิมม์...";
			mes " น้ำหนักของท่านมีมากเกินไป";
			mes " โปรดกลับไปตรวจสอบสัมภาระในตัวท่านแล้วกลับมาหาข้าใหม่อีกครั้ง";
			close;
		}
		Zeny -= .@total_cost;
		delitem 713,.@bottles; //Empty Bottles
		getitem 519,.@bottles; //Milk
		close;
	}
	close;
}
