//===== Hercules Script ======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed OnTouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes " เดี๋ยวๆ !!";
		mes " ตอนนี้น้ำหนักของท่านเกินกำหนด";
		mes " กลับไปตรวจสอบไอเท็มภายในตัวละครของท่านแล้วค่อยกลัยมาใหม่อีกครั้ง!";
		close;
	}
	mes "[Seiyablem]";
	mes " ข้าคือวิศวรกรผู้เชี่ยวชาญ";
	mes " ในการเจารู";
	mes "อาวุธและชุดเกราะธรรมดาๆ";
	mes " บางคนอาจจะถามข้าว่าทำไมมันถึงยุ่งยากนักในการที่จะทำให้มันเป็นรู";
	mes " แต่ข้ามักจะบอกว่า ถึงบอกพวกเจ้าไปพวกเจ้าก็คงจะไม่เข้าใจ";
	next;
	mes "[Seiyablem]";
	mes " จริงๆ แล้ว ข้าว่าเจ้าต้องไม่เคยเห็นใครที่";
	mes " สามารถทำได้อย่างข้ามาก่อนแน่นอน มันพิสูจน์ได้อย่างชัดเจน";
	mes " ว่างานเจาะรูเนี้ย มันยากลำบากแค่ไหน";
	mes " แล้วเจ้ามีสิ่งของที่เจ้าต้องการจะเจารูมั๊ยล่ะ?";
	next;
	switch(select("Add Slot to Weapon:Add Slot to Armor:Slot Attempt Info:ยกเลิก"))
	{
		case 1:
			mes "[Seiyablem]";
			mes " เจ้าบอกว่า Weapon ใช่มั้ย? แน่ใจนะ Ok ไม่มีปัญหา";
			mes " Weapon นั้นแบ่งเป็น 4 กลุ่ม";
			mes " ขึ้นอยู่กับความยากในการเจาะ";
			mes " กลุ่ม C เป็นกลุ่มที่ง่ายที่สุด";
			mes " และกลุ่ม S เป็นกลุ่มที่ยากมากที่สุด";
			mes " เจ้าต้องการจะเลือกกลุ่มไหนดีล่ะ?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes " กลุ่ม C เหรอ อา เข้าใจละ แล้ว Weapon ที่เจ้านำมาคืออะไรล่ะ?";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes " เจ้าเลือกกลุ่ม B ระดับปานกลาง?";
					mes " แล้ว Weapon";
					mes "ที่เจ้านำมาคืออะไรล่ะ?";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "A class หรือ ไม่มีปัญหา";
					mes " เจ้าอยากจะเจาะรูไอเท็มอะไรล่ะ";
					next;
					switch(select("Hunter Bow:Survivor's Rod(INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes " อะ...อะไรนะ S class งั้นหรือ";
					mes " งั้นเจ้าอยากจะเจาะรูไอเท็มอะไรล่ะ";
					next;
					switch(select("Gungnir:Poison Knife:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes " เจ้าบอกว่า Armor ใช่มั้ย? แน่ใจนะ Ok ไม่มีปัญหา";
			mes " Armor นั้นแบ่งเป็น 4 กลุ่ม";
			mes " ขึ้นอยู่กับความยากในการเจาะ";
			mes " กลุ่ม C เป็นกลุ่มที่ง่ายที่สุด";
			mes " และกลุ่ม S เป็นกลุ่มที่ยากมากที่สุด";
			mes " เจ้าต้องการจะเลือกกลุ่มไหนดีล่ะ?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes " Class C";
					mes "เจ้าต้องการจะเลือกไอเท็มอะไรล่ะ";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes " เจ้าเลือก Class B?";
					mes " แล้วเจ้ามี Armor ชนิดไหนล่ะ?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "A class งั้นรึ?";
					mes "ดูเหมือนเจ้าจะต้องการสิ่งที่ยากมากเลยนะ แต่ ไม่มีปัญหา";
					mes " ถ้างัน เจ้ามี armor อะไรมาล่ะ?";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes " อะไรนะ....เอ่อ....S class? *Tremble*";
					mes " ถ้างั้น...เจ้าสนใจจะเอา s class armor ชิ้นไหนมาเจารูล่ะ?";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes " ข้าจะใบ้ให้เจ้า";
			mes " ข้าเดินทางเจาะรูอาวุธและเกราะ";
			mes " มาหลายที่แล้ว";
			mes " ขณะที่ข้าทำงานอยู่ในเมือง Morocc นี้";
			mes " ข้าเข้าใจว่าไอเท็มนั้นผ่านการอัพเกรดสถานะมาสูง";
			mes " จะมีโอกาสนในการเจารูได้ง่ายขึ้น";
			next;
			mes "[Seiyablem]";
			mes " มันอาจจะแตกต่างกันน้อยมาก แต่ข้ารู้สึกเหมือนกับ";
			mes " ข้าทำมันได้ดีที่สุดเมื่อข้าทำงานกับไอเท็มที่อัพเกรดมาสูง ๆ รู้ไหมล่ะ";
			mes " แน่นอน";
			mes "มันมีความเสี่ยงมากที่จะสูญเสียอุปกรณ์สวมใส่";
			mes " หากพลาดในการเจาะรู";
			mes " ข้าต้องบอกเกี่ยวกับโอกาสในการสำเร็จ";
			next;
			mes "[Seiyablem]";
			mes " บางเมืองจะมีโอกาสสำเร็จสูง";
			mes " ในการเจาะรูลงในไอเท็มสวมใส่ที่อัพเกรดมาน้อย";
			mes " โอ้ Morroc ไม่ใช่หนึ่งในนั้นหรอกนะ";
			next;
			mes "[Seiyablem]";
			mes " และสิ่งหนึ่งที่เจ้าต้องตระหนักถึง";
			mes " ข้าจะให้บททดสอบเจ้า";
			mes " พูดสิ เจ้ามี +7 Manteau และ Manteau ปกติ";
			mes " และเจ้าขอร้องให้ข้าเจาะรู Manteau 1 อันที่เจ้ามี";
			mes " ดังนั้น";
			mes "เจ้าคิดว่าข้าต้องการจะทำงานกับอันไหนล่ะ?";
			next;
			mes "[Seiyablem]";
			mes " ถูกต้อง มันจะเป็นการสุ่ม";
			mes " ถ้าเจ้ามีไอเท็มชนิดเดียวกัน";
			mes " มากกว่า 1 ชิ้นใน inventory ของเจ้า";
			mes " ข้าจะสุ่ทเลือก 1 อัน มาเจาะรู";
			mes " ไม่ว่าเจ้าจะต้องการอันไหนหรือไม่";
			next;
			mes "[Seiyablem]";
			mes " ^FF0000ดังนั้น";
			mes "ถ้าเจ้าต้องการเจาะรูไอเท็มสวมใส่ชิ้นไหนโดยเฉพาะ";
			mes "";
			mes "เจ้าต้องนำเฉพาะไอเท็มชิ้นนั้นมาหาข้า";
			mes " และเก็บไอเท็มชนิดเดียวกันชิ้นอื่น ๆ เข้าช่องเก็บของส่วนตัวไป^000000";
			next;
			mes "[Seiyablem]";
			mes " ถ้าเจ้ายังไม่ฟังข้า";
			mes " และนำไอเท็มชนิดเดียวกันมามากว่า 1 ชิ้น";
			mes " ข้าจะไม่รับผิดชอบผลลัพธ์ที่เจ้าไม่ต้องการนะ";
			next;
			mes "[Seiyablem]";
			mes " เจ้าต้องการคำอธิบายอีกหรือไม่?";
			mes " เห็นแล้วจึงจะเชื่อ! ดังนั้น";
			mes "ทำไม่เจ้าไม่ลองเจารู";
			mes " และช่วยให้ข้าดำรงชีวิตอยู่ล่ะ หืม?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes " อืมม!";
			close;
	}
}

function	script	Func_Socket	{

	disable_items;
	mes "[Seiyablem]";
	mes "" + getitemname(getarg(0)) + " เหรอ? เจ้าต้องนำ";
	mes "^FF0000" + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + "^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getarg(8,0) + " " + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "s":"") + "^000000 ") : "") + "and my ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 zeny^000000";
	mes " และที่แน่ๆ อย่าลืมนำ " + getitemname(getarg(0)) + " มาให้ข้าด้วยล่ะ!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(getarg(0),5)&2) // EQP_HAND_R = 2, it's a weapon
	{
		mes " เจ้านำของมาด้วยรึเปล่า?";
		mes " เออ ข้ามีอะไรจะบอกเจ้าสักหน่อยถ้าการเจาะรูล้มเหลวล่ะก็";
		mes " ไอเท็มที่นำมาเจาะ และวัตุดิบที่ใช้เจาะ จะหายไปทันที";
		mes " และเช่นกันถ้า Weapon ชิ้นนั้นเจ้าใส่การ์ดไว้";
		mes " และเจ้าสามารถเจาะรูได้สำเร็จ การ์ดที่เจ้าใส้ก็จะหายไปด้วย";
	}
	else // armor
	{
		mes " เจ้านำของมาด้วยรึเปล่า?";
		mes " เออ ข้ามีอะไรจะบอกเจ้าสักหน่อยถ้าการเจาะรูล้มเหลวล่ะก็";
		mes " ไอเท็มที่นำมาเจาะ และวัตุดิบที่ใช้เจาะ จะหายไปทันที";
		mes " และเช่นกันถ้า Weapon ชิ้นนั้นเจ้าใส่การ์ดไว้";
		mes " และเจ้าสามารถเจาะรูได้สำเร็จ การ์ดที่เจ้าใส้ก็จะหายไปด้วย";
	}
	next;
	switch(select(" ทำการเจารู: ไว้โอกาสหน้าละกัน"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes " เอาล่ะ ข้าจะเริ่มงานเดี๋ยวนี้!";
				mes " เจ้าควรจะภาวนาให้มันสำเร็จ";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes " ยอดเยี่ยม.. !!";
					mes " ดูเหมือนว่าข้าจะทำมันได้สำเร็จ ยินดีด้วย!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					Zeny -= getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes " รับของ ของเจ้าไป!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes " อ๊า... ข้าเสียใจด้วยมันล้มเหลว";
					mes " เจ้าอย่าเสียใจไป..จงพยามใหม่ในครั้งหน้า";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					Zeny -= getarg(4)*1000;
					next;
					mes "[Seiyablem]";
					mes " ข้าหวังว่าเจ้าคงจะโชคดีในครั้งต่อไป!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes " เจ้างงอะไรอีกนี่?";
				mes "เจ้าไม่ได้นำของเหล่านั้นมาเหรอ! ";
				mes " ไปเอามันมาไวๆ เลย";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes " แล้วเจอกันใหม่นะ";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	4_M_02
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	4_M_02
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	4_M_02
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	4_M_02

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes " เจาะรูในไอเท็มสวมใส่";
	mes " อาจทำให้คุณสมบัติไอเท็มใส่ด้วย";
	next;
	mes "[Young Man]";
	mes "Ohมันเป็นเฉพาะสิ่งที่พิเศษเท่านั้น";
	mes " แต่บางที เจ้าสามารถเจาะรูได้";
	mes " 2 slots ในไอเท็มชิ้นเดียวกัน";
	mes " แน่นอน ไม่ใช่ว่าจะทำได้ทุก ๆ ชิ้นหรอกนะ";
	next;
	mes "[Young Man]";
	mes " ข้าคิดจะเรียนรู้อย่่างจริงจัง";
	mes " และกลายเป็นช่างเจาะรู";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	4W_M_01
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	4W_M_01
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	4W_M_01
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	4W_M_01
