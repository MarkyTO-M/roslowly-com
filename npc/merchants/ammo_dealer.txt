//===== Hercules Script ======================================
//= Bullet Dealers
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 1.5
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//= 1.5 Updated to match AEGIS script again. [Masao]
//= 1.6 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//============================================================

alberta,118,160,3	script	Bullet Dealer Tony#alb::bdt	4_M_04,{
	if (BaseJob == Job_Gunslinger) {
		mes "[Tony]";
		mes " เฮ้ ข้า คือ Tony";
		mes " ข้าขายลูกกระสุน";
		mes " และทำลูกกระสุนให้กับ Gunslingers";
		mes " เลือกสิ่งที่ท่านต้องการได้เลย";
		next;
		switch(select("Poison Sphere:Flare Sphere:Lighting Sphere:Blind Sphere:Freezing Sphere:ยกเลิก")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		case 6:
			mes "[Tony]";
			mes " หากท่านเปลี่ยนใจ?";
			mes " กลับมาหาได้ที่นี่ทุกเมื่อที่ท่านต้องการ";
			close;
		}
	}
	mes "[Tony]";
	mes " เฮ้ ข้า คือ Tony";
	mes " ข้าขายลูกกระสุน";
	mes " และทำลูกกระสุนให้กับ Gunslingers";
	mes " มันก็เป็นสิ่งที่";
	mes " พวกเราและสมาคมของเรา";
	mes " ชอบทำกันนี่นะ";
	next;
	mes "[Tony]";
	mes " ต้องขอโทษเจ้าด้วยนะ";
	mes " ถ้าเจ้าจะมาซื้อลูกกระสุน";
	mes " ข้าจะการค้ากับ Gunslingers";
	mes " เท่านั้น!!";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes " ทุกๆ "+getitemname(getarg(2))+" 30 ea";
	mes " ท่านต้องมี";
	mes "1 Emveretarcon,";
	mes "1 Phracon, and";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+".";
	next;
	mes "[Tony]";
	mes " และโปรดจำไว้ว่า ข้าสามารถแลกเปลี่ยนได้ครั้งละไม่เกิน 500 set เท่านั้น";
	mes " ใส่จำนวนที่ท่านต้องการได้เลย หากยกเลิกให้ใส่ ''0''";
	next;
	input .@amount;
	if (.@amount < 1 || .@amount > 500) {
		mes "[Tony]";
		mes " เฮ้ ข้าเตือนท่านไปแล้วไม่ใช่หรือไง? ห้ามเกิน 500 set ต่อครั้งนะ";
		close;
	}
	if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "[Tony]";
			mes " ท่านมีน้ำหนักในตัวมากเกินไป";
			close;
		} else {
			mes "[Tony]";
			mes " ยอดเยี่ยม ท่านนำมันมาทั้งหมด";
			mes " และนี่คือสำหรับท่าน ขอบคุณที่ใช้บริการ~";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	} else {
		mes "[Tony]";
		mes " เอ๊ะ รายการไอเท็มของท่านมีไม่ครบ โปรดตรวจสอบสัมภาระในตัวะครของท่านก่อนใช้บริการ";
		close;
	}
}
