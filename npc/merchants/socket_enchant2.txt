//===== Hercules Script ======================================
//= Episode 12 Socket Enchant NPC
//===== By: ==================================================
//= Gepard
//===== Current Version: =====================================
//= 1.1a
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes " เดี๋ยวๆ !!";
		mes " ตอนนี้น้ำหนักของท่านเกินกำหนด";
		mes " กลับไปตรวจสอบไอเท็มภายในตัวละครของท่านแล้วค่อยกลัยมาใหม่อีกครั้ง";
		close;
	}
	mes "[Leablem]";
	mes " สวัสดี ข้าชื่อ Leablem";
	mes " และข้าก็เป็นผู้เชี่ยวชาญด้าน";
	mes " การเพิ่ม slot";
	mes "ใหกับอาวุธและเครื่องป้องกัน";
	mes " บางครั้งมันก็เป็นเรื่องยากมาก";
	mes " แต่ข้าก็ชอบในสิ่งที่ข้าทำและก็ภูมิใจกับมัน";
	next;
	mes "[Leablem]";
	mes " ข้าได้เรียนรู้เกือบทุกอย่างมาจากพี่ชายข้า Seyablem";
	mes " แต่ก็ยังมีสิ่งที่ต้องเรียนรู้อีกมากมาย";
	mes " ดังนั้นโปรดเข้าใจด้วยว่าการเจาะรูไอเท็ม";
	mes " นั้นยังมีข้อจำกัดไม่เหมือนกับพี่ข้า Seyablem";
	next;
	mes "[Leablem]";
	mes " พี่ข้าไม่ค่อยชอบใจนักเรื่องสอน";
	mes " ความสามารถของเขาให้ข้าเพราะเขา";
	mes " กังวลว่าเขาจะไม่ใช่คนเดียวที่ทำได้อีกต่อไป แต่อย่างไรก็ตาม";
	mes " เขาก็สอนอย่างเต็ม";
	next;
	mes "[Leablem]";
	mes " ขอบคุณเขามากที่สอนให้ข้าเรียนรู้หลายอย่าง";
	mes " ข้าไม่ค่อยมั่นใจมาก";
	mes " ในการทดลองทำหลายๆ อย่าง";
	mes " แต่ข้าก็จะทำ";
	mes " เท่าที่ข้าจะทำได้";
	next;
	mes "[Leablem]";
	mes " จริงๆ แล้วมีอยู่อย่างหนึ่ง";
	mes " ที่พี่ข้าไม่อยากให้ทำ";
	mes " ข้าไม่อยากเสียให้เขาเสียใจ...ดังนั้นข้ามเรื่องนั้นไปดีกว่า";
	mes " ข้าไม่อยากจะพูดถึงมันอีก!";
	next;
	mes "[Leablem]";
	mes " ฃ่างเถอะ แล้วเจ้ามี";
	mes " อะไรมาให้ข้าเจาะรูล่ะ?";
	mes " ค่าบริการ วัสดุ";
	mes " และโอกาสสำเร็จ";
	next;
	switch(select(" อาวุธ: เครื่องป้องกัน: สิ้งที่เจ้าไม่อยากพูดถึง: ข้อมูลอื่นๆ: ยกเลิก"))
	{
		case 1:
			mes "[Leablem]";
			mes " เจ้าบอกว่า Weapon ใช่มั้ย? แน่ใจนะ Ok ไม่มีปัญหา";
			mes " Weapon นั้นแบ่งเป็น 4 กลุ่ม";
			mes " ขึ้นอยู่กับความยากในการเจาะ";
			mes " กลุ่ม C เป็นกลุ่มที่ง่ายที่สุด";
			mes " และกลุ่ม S เป็นกลุ่มที่ยากมากที่สุด";
			mes " เจ้าต้องการจะเลือกกลุ่มไหนดีล่ะ?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes " กลุ่ม C เหรอ อา เข้าใจละ แล้ว Weapon ที่เจ้านำมาคืออะไรล่ะ?";
					next;
					switch(select("Book of Mother Earth:Book of Billows:Book of Gust of Wind:Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes " เจ้าเลือกกลุ่ม B ระดับปานกลาง?";
					mes " แล้ว Weapon";
					mes "ที่เจ้านำมาคืออะไรล่ะ?";
					next;
					switch(select("Orcish Axe:Scimiter:Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "A class หรือ ไม่มีปัญหา";
					mes " เจ้าอยากจะเจาะรูไอเท็มอะไรล่ะ";
					next;
					switch(select("Dragon Killer:Katar of Quaking:Katar of Raging Blaze:Katar of Frozen Icicle:Katar of Piercing Wind:Golden Mace:Oriental Lute:Queen's Whip:Spectral Spear:Gae Bolg:Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes " อะ...อะไรนะ S class งั้นหรือ";
					mes " งั้นเจ้าอยากจะเจาะรูไอเท็มอะไรล่ะ";
					next;
					switch(select("Zephyrus:Mailbreaker:Dragon Slayer:Swordbreaker:Assasin Dagger:Grand Cross:Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes " เจ้าบอกว่า Armor ใช่มั้ย? แน่ใจนะ Ok ไม่มีปัญหา";
			mes " Armor นั้นแบ่งเป็น 4 กลุ่ม";
			mes " ขึ้นอยู่กับความยากในการเจาะ";
			mes " กลุ่ม C เป็นกลุ่มที่ง่ายที่สุด";
			mes " และกลุ่ม S เป็นกลุ่มที่ยากมากที่สุด";
			mes " เจ้าต้องการจะเลือกกลุ่มไหนดีล่ะ?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes " Class C";
					mes "เจ้าต้องการจะเลือกไอเท็มอะไรล่ะ";
					next;
					switch(select("Sunflower:Ph.D Hat:Big Ribbon:Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes " เจ้าเลือก Class B?";
					mes " แล้วเจ้ามี Armor ชนิดไหนล่ะ?";
					next;
					switch(select("Skull Ring:High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "A class งั้นรึ?";
					mes "ดูเหมือนเจ้าจะต้องการสิ่งที่ยากมากเลยนะ แต่ ไม่มีปัญหา";
					mes " ถ้างัน เจ้ามี armor อะไรมาล่ะ?";
					next;
					switch(select("Pirate Bandana:Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
					}
				case 4:
					mes "[Leablem]";
					mes " อะไรนะ....เอ่อ....S class? *Tremble*";
					mes " ถ้างั้น...เจ้าสนใจจะเอา s class armor ชิ้นไหนมาเจารูล่ะ?";
					next;
					switch(select("Mage Coat:Holy Robe:Sacred Mission:Undershirt:Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes " โอ้ มันไม่ได้พิเศษอะไรหรอก";
			mes " แต่พี่ข้่าคิดว่ามันไม่ใช่สิ่งที่มนุษย์ควรจะไปหลงมัน";
			mes " ข้าไม่อยากทำให้พี่ข้าเสียใจ";
			mes " ข้าควรลืมมันไปเสียดีกว่า";
			next;
			mes "[Leablem]";
			mes "...โอกาสสำเร็จนั้นมีอยู่มากอยู่";
			mes " แต่ใช้วัสดุและเงินเยอะมาก";
			mes " ข้าก็ไม่คิดว่าใครจะบ้าทำ";
			mes " ถึงแม้จะเปิดให้บริการเอาไว้";
			next;
			mes "[Leablem]";
			mes "...อื้มม...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes " ดูเหมือนเจ้าจะสนใจหรือ";
			mes " ถ้าเจ้าต้องการข้าบอกเจ้าได้ ";
			mes " แต่เจ้าสัญญาว่าจะเก็บเป็นความลับ";
			mes " ถ้าพี่ข้ารู้ว่าบอกเจ้าละก็";
			mes " เขาเอาข้าตายแน่!";
			next;
			mes "[Leablem]";
			mes " บริการนี้มีโอกาสสำเร็จถึง 90%";
			mes " แต่ต้องใช้ 2 Gold และ 2 ร้อยล้าน zeny:";
			mes " ข้าสามารถเจาะรูให้กับ Hat of the Sun God";
			next;
			mes "[Leablem]";
			mes " พี่ข้าเคยนบอกว่า ตั้งแต่โบราณมีกฏห้ามสิ่งนี้เอาไว้";
			mes " แต่ข้าเชื่อว่ามนุษย์มีความอยากรู้อยากเห็น";
			mes " และชอบทดลอง โดยเฉพาะสิ่งที่ถูกห้าม";
			mes " ข้าก็แค่ไม่อยากให้พี่ข้ามาด่าข้าแค่นั้นแหละ";
			next;
			switch(select(" ข้าจะทำ"," ยกเลิก"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...อะ... อะไรนะ? ไม่!";
					mes " นี่จะให้ข้าเป็นคนไม่ดีหรือ";
					mes " อย่าทำกับข้าแบบนี้!";
					mes " ไม่ ข้าไม่ทำ! ไม่คือไม่! ไม่!";
					next;
					mes "[Leablem]";
					mes " ไม่ ข้าจะไม่! ไม่!";
					next;
					mes "[Leablem]";
					mes " ข้าไม่สามารถทำมันได้...";
					next;
					mes "[Leablem]";
					mes " ข้าไม่สามารถทำ... ได้...";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes " อื้มมม...";
					next;
					mes "[Leablem]";
					mes " เจ้านี่ดื้อจริงๆ ก็ได้ข้าจะยกเว้นให้เจ้าสักคน";
					mes " อย่าบอกใครล่ะ";
					mes " โดยเฉพาะพี่ชายข้า สัญญาได้ไหม?";
					next;
					mes "[Leablem]";
					mes " งั้นก็ นำสิ่งเหล่านี้มา";
					mes "^FF00002 Gold, 2 ร้อยล้าน xzeny -- ค่าบริการ --";
					mes "^FF0000และก็ Hat of the Sun God.";
					mes "^FF0000โอกาสสำเร็จมี 90% เจ้ารู้ไหม?^000000";
					mes " เวลามันมีค่า";
					mes " รีบๆ ลงมือเลยดีกว่า";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "พร้อมที่จะเริ่มล่ะนะ?";
						mes "ขอให้สิ่งศักสิทธิ์ทั้งหลายคุ้มครองท่าน";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes " ยอดเยี่ยมจริงๆ ! ข้าคิดว่าโอกาสสำเร็จ 90% นั้นคือเรื่องจริง";
							mes " ดูสิ slot ได้ถูกเพิ่มเข้ามาแล้ว";
							mes " ยินดีด้วย";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							Zeny -= 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes " นี่เป็นครั้งแรกและครั้งสุดท้ายที่ข้าจะทำเช่นนี้อีก";
							mes " แล้วอย่าบอกใครล่ะ เกี่ยวกับเรื่องนี้?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes " ว๊าาา! คุณพระช่วย ล้มเหลวรึ!";
							mes " ข้าเดาไว้แต่แรกแล้ว เพราะข้ามันโชคร้าย..";
							mes " ข้าทำอะไรลงไป.. นี่ล่ะคือเหตุผลที่ข้าไม่อยากทำมัน";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							Zeny -= 200000000;
							next;
							mes "[Leablem]";
							mes " แล้วท่านอย่ามาขอให้ข้าเสี่ยงกับเรื่องแบบนี้อีกเลย!";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "อืม...";
						next;
						mes "[Leablem]";
						mes " เจ้ามีวัตถุดิบไม่พอ";
						mes " แล้วจะให้ข้าลองอะไร?";
						mes " ข้าจะไม่ทำให้เจ้าอีกแล้ว";
						mes " ถึงแม้เจ้าจะมีวัตถุดิบครบก็ตาม เฮ้อ~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes " อย่าบอกใคร..";
					mes " เกี่ยวกับเรื่องนี้นะ..!?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes " อยากรู้รายละเอียดเพิ่มหรือ?";
			mes " อืม ข้าไม่คิดว่าจะมีอะไร";
			mes " ที่ต้องรู้เป็นพิเศษนะ";
			mes " แต่ข้าแนะนำเจ้าได้";
			next;
			mes "[Leablem]";
			mes " การเจารูไอเท็มในแต่ละเมือง";
			mes " โอกาสในการเจาะสำเร็จ";
			mes " ขึ้นอยู่กับระดับของอุปกรณ์";
			mes " ข้าไม่รู้ว่าจริงไหม";
			mes " แต่ข้าเห็นมันแตกต่างกันทุกเมือง";
			next;
			mes "[Leablem]";
			mes " อย่างเช่น ช่างเจาะรูเมืองหนึ่ง";
			mes " มีโอกาสเจาะรูสำเร็จ";
			mes " กับอาวุธที่อัพเกรดสูง";
			mes " ในขณะที่ช่างเจาะรูอีกเมืองหนึ่ง";
			mes " เจาะสำเร็จกับอาวุธที่อัพเกรดต่ำ";
			next;
			mes "[Leablem]";
			mes " เมื่อเจ้าล้มเหลวในการเจาะรูไอเท็ม";
			mes " ไอเท็มชิ้นนั้นก็จะหายไป";
			mes " ข้าก็พยามเต็มที่นะ";
			mes " แต่งานเจาะรูไอเท็มมันเป็นอะไรที่ซับซ้อน";
			next;
			mes "[Leablem]";
			mes " และสิ่งหนึ่งที่เจ้าต้องตระหนักถึง";
			mes " ข้าจะให้บททดสอบเจ้า";
			mes " พูดสิ เจ้ามี +7 Manteau และ Manteau ปกติ";
			mes " และเจ้าขอร้องให้ข้าเจาะรู Manteau 1 อันที่เจ้ามี";
			mes " ดังนั้น";
			mes "เจ้าคิดว่าข้าต้องการจะทำงานกับอันไหนล่ะ?";
			next;
			mes "[Leablem]";
			mes " ถูกต้อง มันจะเป็นการสุ่ม";
			mes " ถ้าเจ้ามีไอเท็มชนิดเดียวกัน";
			mes " มากกว่า 1 ชิ้นใน inventory ของเจ้า";
			mes " ข้าจะสุ่ทเลือก 1 อัน มาเจาะรู";
			mes " ไม่ว่าเจ้าจะต้องการอันไหนหรือไม่";
			next;
			mes "[Leablem]";
			mes " ^FF0000ดังนั้น";
			mes "ถ้าเจ้าต้องการเจาะรูไอเท็มสวมใส่ชิ้นไหนโดยเฉพาะ";
			mes "";
			mes "เจ้าต้องนำเฉพาะไอเท็มชิ้นนั้นมาหาข้า";
			mes " และเก็บไอเท็มชนิดเดียวกันชิ้นอื่น ๆ เข้าช่องเก็บของส่วนตัวไป^000000";
			next;
			mes "[Leablem]";
			mes " ถ้าเจ้ายังไม่ฟังข้า";
			mes " และนำไอเท็มชนิดเดียวกันมามากว่า 1 ชิ้น";
			mes " ข้าจะไม่รับผิดชอบผลลัพธ์ที่เจ้าไม่ต้องการนะ";
			next;
			mes "[Leablem]";
			mes " เจ้าต้องการคำอธิบายอีกหรือไม่?";
			mes " เห็นแล้วจึงจะเชื่อ! ดังนั้น";
			mes "ทำไม่เจ้าไม่ลองเจารู";
			mes " และช่วยให้ข้าดำรงชีวิตอยู่ล่ะ หืม?";
			close;
		case 5:
			mes "[Leablem]";
			mes "ก็แล้วแต่เจ้า..";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	4_M_04
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	4_M_04
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	4_M_04
moc_ruins,154,86,3	duplicate(SocketEnchant2)	Leablem#moc	4_M_04

function	script	Func_Socket2	{

	disable_items;
	mes "[Leablem]";
	if(getarg(7,0) != 0 && getarg(8,0) != 0)
	{
		mes getitemname(getarg(0)) + ".. และนี่คือสิ่งของที่ต้องนำมาให้ข้า";
		mes "^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 เซนี สำหรับค่าบริการ";
		mes "และ ^FF0000" + getarg(6) + "ea " + getitemname(getarg(5)) + " ea และ " + getarg(8) + "ea " + getitemname(getarg(7)) + "";
		mes "^FF0000และแน่นอนจงอย่าลืม " + getitemname(getarg(0)) + ".^000000";
	}
	else
	{
		mes getitemname(getarg(0)) + "? และนี่คือสิ่งของที่ต้องนำมาให้ข้า";
		mes "^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000  เซนี สำหรับค่าบริการ " + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + " สำหรับวัตถุดิบ";
		mes "^FF0000และแน่นอนจงอย่าลืม " + getitemname(getarg(0)) + "";
	}
	next;
	mes "[Leablem]";
	mes " ตอนนี้มีไอเท็มครบทั้งหมดแล้ว?";
	mes " และหากท่านล้มเหลวในการเจาะรู";
	mes " ท่านจะต้องสูเสียเสียไอเท็ม " + (getiteminfo(getarg(0),5)&2 ? "weapon" : "armor") + "."; // EQP_HAND_R = 2, it's a weapon, otherwise armor
	mes " ถ้าท่านยังจำได้ " + (getiteminfo(getarg(0),5)&2 ? "weapon" : "armor") + " has been upgraded, and has been inserted with a card,";
	mes " การ์ดที่อยู่ภายในไอเท็มก็จะหายไปด้วย";
	next;
	switch(select("ทำการเจารู: ไว้โอกาสหน้าละกัน"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes " เอาล่ะ ข้าจะเริ่มงานเดี๋ยวนี้!";
				mes " เจ้าควรจะภาวนาให้มันสำเร็จ";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes " ยอดเยี่ยม.. !!";
					mes " ดูเหมือนว่าข้าจะทำมันได้สำเร็จ ยินดีด้วย!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					Zeny -= getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes " แล้วเจอกันใหม่ สหาย!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes " อ๊า... ข้าเสียใจด้วยมันล้มเหลว";
					mes " เจ้าอย่าเสียใจไป..จงพยามใหม่ในครั้งหน้า";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					Zeny -= getarg(4)*1000;
					next;
					mes "[Leablem]";
					mes " ข้าหวังว่าเจ้าคงจะโชคดีในครั้งต่อไป!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes " เจ้างงอะไรอีกนี่?";
				mes "เจ้าไม่ได้นำของเหล่านั้นมาเหรอ! ";
				mes " ไปเอามันมาไวๆ เลย";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes " แล้วเจอกันใหม่นะ";
			close;
	}
}
