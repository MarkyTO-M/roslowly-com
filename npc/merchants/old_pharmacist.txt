//===== Hercules Script ======================================
//= Old Pharmacist
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.4
//===== Description: =========================================
//= Trade in items for potions.
//= Breakdown of arguments used:
//= arg(0): Herb required.
//= arg(1): Zeny cost.
//= arg(2): Potion given.
//===== Additional Comments: =================================
//= 1.0 Fully working. [DZeroX]
//= 1.0a Minor fixes [Lupus]
//= 1.1 Added changed the dialogs and name to fit the
//=     the official information. [DZeroX]
//= 1.1a Removed .GATs [Lupus]
//= 1.1b Fixed a bug in Orange Potion creating. Copy&Pasting = bad. [L0ne_W0lf]
//= 1.2 Added checkweight, and input min/max values. [L0ne_W0lf]
//= 1.3 Fixed minor condition check bug. (bugreport:597) [L0ne_W0lf]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//============================================================

alberta_in,16,28,4	script	ปู่นักปรุงยา	1_M_PUBMASTER,{
	if (checkweight(1201,1) == 0) {
		mes "- น้ำหนักของเจ้ามีมากเกินไป! -";
		close;
	}
	mes "[Old Pharmacist]";
	mes " เอ่อ... เจ้าเป็นใคร... ?";
	switch(select(" ทำ Potion"," พูดคุย"," ขอข้อมูล"," ยกเลิก")) {
	case 1:
		next;
		if (MaxWeight - Weight < 5000) {
			mes "[ปู่นักปรุงยา]";
			mes " ข้าให้เจ้าไปมากกว่านี้ไม่ได้ น้ำหนักของเจ้ามีมากเกินไป";
			close;
		} else {
			mes "[ปู่นักปรุงยา]";
			mes " เจ้าเตรียมไอเท็มที่จำเป็นมาครบหรือเปล่า? เจ้าต้องการ potion ชนิดไหน?";
			switch(select("Red Potion.:Orange Potion.:Yellow Potion.:White Potion.:Blue Potion.:Green Potion.:เอ่อ.. ไม่มีอะไร")) {
			case 1:
				callsub L_Making,507,3,501;
			case 2:
				next;
				mes "[ปู่นักปรุงยา]";
				if (countitem(507) < countitem(713)) {
					set .@max,countitem(507);
				}
				else if (countitem(508) < countitem(713)) {
					set .@max,countitem(508);
				}
				else {
					set .@max,countitem(713);
				}
				if ((countitem(507) < 1) || (countitem(508) < 1) || (countitem(713) == 0)) {
					mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำไอเท็มมาไม่ครบ!";
					mes " ไปให้พัน!";
					close;
				}
				if (Zeny < 3) {
					mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำเงินมาไม่ครบ!";
					mes " ไปให้พัน!";
					close;
				}
				mes " ต้องการเท่าไหร่ล่ะ?";
				switch(select(" เอาเท่าที่? ทำได้"," เอ่อ ? จะระบุตัวเลขเอง"," ไม่ล่ะ ลืมมันซะเถอะ")) {
				case 1:
					next;
					if ((countitem(507) < .@max) || (countitem(508) < .@max) || (countitem(713) < .@max) || (Zeny < .@max*3)) {
						mes "[ปู่นักปรุงยา]";				
						mes " เจ้าอย่าหวังจะเอาอะไรไปจากข้าเมื่อเจ้านำไอเท็มมาไม่ครบ!";
						mes " ไปให้พัน!";
						close;
					}
					Zeny -= (.@max*5);
					delitem 507,.@max; //Red_Herb
					delitem 508,.@max; //Yellow_Herb
					delitem 713,.@max; //Empty_Bottle
					getitem 502,.@max; //Orange_Potion
					break;
				case 2:
					next;
					mes "[ปู่นักปรุงยา]";
					mes " ใส่ตัวเลขที่ 100 หรือต่ำกว่า ถ้าจะยกเลิกให้ใส่ '0' เจ้าจะได้ Potion สูงสุด"+.@max+" ขวด";
					input .@amount,0,101;
					if (.@amount == 0) {
						next;
						mes "[ปู่นักปรุงยา]";
						mes " ตามใจเถอะ?!";
						close;
					}
					if (.@amount > 100) {
						next;
						mes "[ปู่นักปรุงยา]";
						mes " ข้าบอกแล้วว่าสูงสุดเพียง 100 เท่านั้น!";
						close;
					}
					if ((countitem(507) < .@amount) || (countitem(508) < .@amount) || (countitem(713) < .@amount) || (Zeny < .@amount*3)) {
						next;
						mes "[ปู่นักปรุงยา]";				
						mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำไอเท็มมาไม่ครบ!";
						mes " ไปให้พัน!";
						close;
					}
					next;
					Zeny -= (.@amount*5);
					delitem 507,.@amount; //Red_Herb
					delitem 508,.@amount; //Yellow_Herb
					delitem 713,.@amount; //Empty_Bottle
					getitem 502,.@amount; //Orange_Potion
					break;
				case 3:
					next;
					mes "[ปู่นักปรุงยา]";
					mes " อะไร?!";
					mes " อ้าา..";
					mes " !!!";
					mes " ไปให้พ้น!";
					close;
				}
				mes "[ปู่นักปรุงยา]";
				mes " เอ้านี่ เอาไปซะ แต่จำไว้อย่า กินยามากไป มันไม่ดีต่อสุขภาพ";
				close;
			case 3:
				callsub L_Making,508,10,503;
			case 4:
				callsub L_Making,509,20,504;
			case 5:
				callsub L_Making,510,30,505;
			case 6:
				callsub L_Making,511,3,506;
			case 7:
				mes "[ปู่นักปรุงยา]";
				mes " อะไร?!";
				mes " อ้าา..";
				mes " !!!";
				mes " ไปให้พ้น!";
				close;
			}
		}
	case 2:
		mes "[ปู่นักปรุงยา]";
		mes " เจ้าสามารถรักษาโรคได้ด้วยา แต่เจ้าต้องระวังในการใช้ยาไม่อย่างนั้น เจ้าอาจจะติดยาได้...";
		mes " ฮู่วววว...ข้าแก่เกิกว่าจะพูดนานๆได้";
		mes " ยังไงก็แล้วแต่";
		mes "นำยาเป็นยาที่จะช่วยเหลือเจ้าเมื่อเจ้าหมดพละกำลัง ก็แค่นั้น";
		close;
	case 3:
		mes "[ปู่นักปรุงยา]";
		mes " ฮั่กฮั่ก... ทำไมมากวนข้าเช่นนี้... ก็ได้ข้าจะอธิบายการใช้น้ำยา";
		next;
		mes "[ปู่นักปรุงยา]";
		mes " เจ้าสามารถกิสมุนไพรธรรมดาได้เอง แต่ในขณะเดียวกันถ้าเจ้ากินมันหลังจากที่มันเป็นน้ำยำแล้ว เจ้าจะฟื้นฟูฟลังงานได้มากกว่าปกติ";
		next;
		mes "[ปู่นักปรุงยา]";
		mes " ข้าเป็นเพียงหนึ่งเดียวที่สามารถทำน้ำยาจากสมุนไพรได้ แต่ข้าได้เงินเพียงเล็กน้อยจากความพยายามของข้า... ข้าต้องการเงินเพื่อเลี้ยงชีพ";
		next;
		mes "[ปู่นักปรุงยา]";
		mes "Red Potion - ^0098E52 Red Herbs, 1 Empty Bottl ค่าจ้าง 3 เซนี^000000";
		mes "Orange Potion - ^0098E51 Red Herb, 1 Yellow Herb, 1 Empty Bottle, ค่าจ้าง 5 เซนี^000000";
		mes "Yellow Potion - ^0098E52 Yellow Herbs, 1 Empty Bottle, ค่าจ้าง 10 เซนี^000000";
		next;
		mes "[ปู่นักปรุงยา]";
		mes "White Potion - ^0098E52 White Herbs, 1 Empty Bottle, ค่าจ้าง 20 เซนี^000000";
		mes "Blue Potion - ^0098E52 Blue Herbs, 1 Empty Bottle, ค่าจ้าง 30 เซนี^000000";
		mes "Green Potion - ^0098E52 Green Herbs, 1 Empty Bottle, ค่าจ้าง 3 เซนี^000000";
		close;
	case 4:
		mes "[ปู่นักปรุงยา]";
		mes " เจ้าไม่มีอะไรพูดเลยหรือ?!";
		close;
	}
	
L_Making:
	next;
	mes "[ปู่นักปรุงยา]";	
	if ((countitem(getarg(0))/2) < countitem(713)) {
		set .@max,countitem(getarg(0))/2;
	} else {
		set .@max,countitem(713);
	}
	if ((countitem(getarg(0)) < 2) || (countitem(713) == 0)) {
		mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำไอเท็มมาไม่ครบ!";
		mes " ไปให้พัน!";
		close;
	}
	if (Zeny < 3) {
		mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำเงินมาไม่ครบ!";
		mes " ไปให้พัน!";
		close;
	}
	mes " ต้องการเท่าไหร่ล่ะ?";
	switch(select(" เอาเท่าที่? ทำได้"," เอ่อ ? จะระบุตัวเลขเอง"," ไม่ล่ะ ลืมมันซะเถอะ")) {
	case 1:
		next;
		if ((countitem(getarg(0)) < .@max*2) || (countitem(713) < .@max) || (Zeny < .@max*getarg(1))) {
			mes "[ปู่นักปรุงยา]";				
			mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำเงินมาไม่ครบ!";
			mes " ไปให้พัน!";
			close;
		}
		Zeny -= (.@max*getarg(1));
		delitem getarg(0),.@max*2;
		delitem 713,.@max; //Empty_Bottle
		getitem getarg(2),.@max; 
		break;
	case 2:
		next;
		mes "[ปู่นักปรุงยา]";
		mes " ใส่ตัวเลขที่ 100 หรือต่ำกว่า ถ้าจะยกเลิกให้ใส่ '0' เจ้าจะได้ Potion สูงสุด"+.@max+" ขวด";
		input .@amount;
		next;
		if (.@amount == 0) {
			mes "[ปู่นักปรุงยา]";
			mes " ตามใจเถอะ?!";
			close;
		}
		if (.@amount > 100) {
			mes "[ปู่นักปรุงยา]";
			mes " ข้าบอกแล้วว่าสูงสุดเพียง 100!";
			close;
		}
		if ((countitem(getarg(0)) < .@amount*2) || (countitem(713) < .@amount) || (Zeny < .@amount*getarg(1))) {
			mes "[ปู่นักปรุงยา]";				
			mes " เจ้าอย่าหวังจะได้อะไรไปจากข้าเมื่อเจ้านำเงินมาไม่ครบ!";
			mes " ไปให้พัน!";
			close;
		}
		Zeny -= (.@amount*getarg(1));
		delitem getarg(0),.@amount*2;
		delitem 713,.@amount; //Empty_Bottle
		getitem getarg(2),.@amount; 
		break;
	case 3:
		next;
		mes "[ปู่นักปรุงยา]";
		mes " อะไร?!";
		mes " อ้าา..";
		mes " !!!";
		mes " ไปให้พ้น!";
		close;
	}
	mes "[ปู่นักปรุงยา]";
	mes " เอ้านี่ เอาไปซะ แต่จำไว้อย่า กินยามากไปมันไม่ดีต่อสุขภาพ";
	close;
}
